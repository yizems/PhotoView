apply plugin: 'maven'

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "${MAVEN_UPLOAD_URL}") {
                authentication(userName: "${MAVEN_USER}", password: "${MAVEN_PWD}")
            }

            pom.groupId = "com.gwmfc.android"
            pom.artifactId = "photo_view"
            pom.version = android.defaultConfig.versionName
            pom.packaging = "aar"
        }
    }
}

//生成文档注释
task androidJavadocs(type: Javadoc) {
    failOnError = false
    source = android.sourceSets.main.java.srcDirs
    ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
    classpath += files(ext.androidJar)
}
//将文档打包成jar
task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}
//将源码打包
task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}
//包含文档和源码的aar
artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}